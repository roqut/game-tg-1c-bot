
#Область ПрограммныйИнтерфейс

// Обработчик элемента очереди в режиме работы бота - Чат игра
// 
// Параметры:
//  ДанныеЭлемента - Структура
Процедура ПриОбработкеЭлементаОчереди(ДанныеЭлемента) Экспорт
	
	Если ДанныеЭлемента.Свойство("message") Тогда
		
		Сообщение = ДанныеЭлемента.message;
		
		Если Не Сообщение.Свойство("from") Тогда
			Возврат;
		КонецЕсли;
		
		Если Не Сообщение.Свойство("text") Тогда
			Возврат;
		КонецЕсли;

		Отправитель = Сообщение.from.id;
		Текст = Сообщение.text;
		
		СостояниеИгрока = ПроверкаСостоянияИгрока(Отправитель);
		ИгрокПодключается = ИгрокПытаетсяПодключиться(Отправитель);
		
		Если Текст = "Мой профиль" и СостояниеИгрока = "ВнеИгры" Тогда
			ПосмотретьМойПрофиль(Отправитель);
		ИначеЕсли Текст = "Создать новую игру" и СостояниеИгрока = "ВнеИгры" Тогда
			СоздатьНовуюСессию(Отправитель);		
		ИначеЕсли Текст = "Присоединиться к игре" и СостояниеИгрока = "ВнеИгры" и не ИгрокПодключается Тогда
			НачатьСоединение(Отправитель);	
		ИначеЕсли Текст = "Выйти из режима ожидания" и СостояниеИгрока = "ОжиданиеИгры" Тогда
			СбросСессии(Отправитель);		
		ИначеЕсли Текст = "Вернуться назад" и СостояниеИгрока = "ВнеИгры" и ИгрокПодключается Тогда
			СбросПодключения(Отправитель);	
		ИначеЕсли ИгрокПодключается и СостояниеИгрока = "ВнеИгры" Тогда
			ПроверкаКодаПодключения(Отправитель, Текст);	
		ИначеЕсли Текст = "Выйти из игры" и (СостояниеИгрока = "КонецИгры"
												или СостояниеИгрока = "Игра" 
												или СостояниеИгрока = "ОжиданиеГотовности"
												или СостояниеИгрока = "ОжиданиеГотовностиПерезапускуИгры") Тогда
			СбросСессионнойИгры(Отправитель);			
		ИначеЕсли Текст = "Начать заново" и (СостояниеИгрока = "КонецИгры"
												или СостояниеИгрока = "Игра"
												или СостояниеИгрока = "ОжиданиеГотовностиПерезапускуИгры") Тогда 
			НачатьИгруЗаново(Отправитель, СостояниеИгрока);
		Иначе
			ПредложитьВыбратьДействия(Отправитель, СостояниеИгрока);
		КонецЕсли;
			
	КонецЕсли;
	
	Если ДанныеЭлемента.Свойство("callback_query") Тогда
		НажатиеКнопки = ДанныеЭлемента.callback_query;
		ОбработатьНажатияКлеткиКнопки(НажатиеКнопки);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПроверкаСостоянияИгрока(Отправитель)
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ЧатИграСессии.СтатусИгры
		|ИЗ
		|	Справочник.ЧатИграСессии КАК ЧатИграСессии
		|ГДЕ
		|	ЧатИграСессии.ПервыйИгрок = &ТекИгрок
		|	ИЛИ ЧатИграСессии.ВторойИгрок = &ТекИгрок";
	Запрос.УстановитьПараметр("ТекИгрок", Отправитель);
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Если Выборка.Следующий() Тогда
		Если Выборка.СтатусИгры = Перечисления.СтатусыИгры.КонецИгры Тогда
			Возврат "КонецИгры";
		ИначеЕсли Выборка.СтатусИгры = Перечисления.СтатусыИгры.Игра Тогда
			Возврат "Игра";
		ИначеЕсли Выборка.СтатусИгры = Перечисления.СтатусыИгры.ОжиданиеГотовности Тогда
			Возврат "ОжиданиеГотовности";
		ИначеЕсли Выборка.СтатусИгры = Перечисления.СтатусыИгры.ОжиданиеГотовностиПерезапускуИгры Тогда 
			Возврат "ОжиданиеГотовностиПерезапускуИгры";
		Иначе
			Возврат "ОжиданиеИгры";
		КонецЕсли;
	Иначе
		Возврат "ВнеИгры";		
	КонецЕсли;
КонецФункции

Функция ИгрокПытаетсяПодключиться(Отправитель)
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ЧатИграПодключениеИгрока.Код
		|ИЗ
		|	Справочник.ЧатИграПодключениеИгрока КАК ЧатИграПодключениеИгрока
		|ГДЕ
		|	ЧатИграПодключениеИгрока.Игрок = &Игрок";
	Запрос.УстановитьПараметр("Игрок", Отправитель);
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;		
	КонецЕсли;
КонецФункции

Процедура ПосмотретьМойПрофиль(Отправитель)

	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	УчетныеЗаписиТелеграм.Наименование КАК Имя,
		|	УчетныеЗаписиТелеграм.ЧислоПобед КАК ЧислоПобед,
		|	УчетныеЗаписиТелеграм.ЧислоПобед + УчетныеЗаписиТелеграм.ЧислоПоражений КАК КолвоИгр,
		|	УчетныеЗаписиТелеграм.ЧислоПоражений КАК КолвоПоражений
		|ИЗ
		|	Справочник.УчетныеЗаписиТелеграм КАК УчетныеЗаписиТелеграм
		|ГДЕ
		|	УчетныеЗаписиТелеграм.Код = &ТекИгрок";
	Запрос.УстановитьПараметр("ТекИгрок", Отправитель);
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	Если не Выборка.Следующий() Тогда
		ИнтеграцияТелеграм.ОтправитьСообщение(Отправитель, "Ошибка, пользователь не найден!");
		Возврат;
	КонецЕсли;

	ШаблонШапка = "<b>Профиль игрока «%1»</b>" + Символы.ПС;
	ШаблонКолвоИгр = "Всего игр: %1";
	ШаблонКолвоПобед = "Количество побед: %1";
	ШаблонПроцентПобед = "Процент побед: %1";
	
	НачалоТэгаИмени = СтрНайти(Выборка.Имя, "(") - 2;
	
	ИмяП = ?(НачалоТэгаИмени > 0, Сред(Выборка.Имя, 1, НачалоТэгаИмени), Выборка.Имя);
	
	ПроцентПобед = ?((Выборка.ЧислоПобед + Выборка.КолвоПоражений) > 0, 
						ОКР((Выборка.ЧислоПобед/(Выборка.ЧислоПобед + Выборка.КолвоПоражений))*100, 0), 
							0);
	
	ВсеСтроки = Новый Массив;
	ВсеСтроки.Добавить(СтрШаблон(ШаблонШапка, ИмяП));
	ВсеСтроки.Добавить(СтрШаблон(ШаблонКолвоИгр, Выборка.КолвоИгр));
	ВсеСтроки.Добавить(СтрШаблон(ШаблонКолвоПобед, Выборка.ЧислоПобед));
	ВсеСтроки.Добавить(СтрШаблон(ШаблонПроцентПобед, ПроцентПобед) + "%");
	
	СтатистикаИгрока =	СтрСоединить(ВсеСтроки, Символы.ПС);
	ИнтеграцияТелеграм.ОтправитьСообщение(Отправитель, СтатистикаИгрока);
	
КонецПроцедуры

Процедура СоздатьНовуюСессию(Отправитель)
	
	СуществующиеКоды = Новый Массив;

	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ЧатИграСессии.КодДляСоединения
		|ИЗ
		|	Справочник.ЧатИграСессии КАК ЧатИграСессии";
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		СуществующиеКоды.Добавить(ВыборкаДетальныеЗаписи.КодДляСоединения);
	КонецЦикла;
	
	НоваяСессия = Справочники.ЧатИграСессии.СоздатьЭлемент();
	НоваяСессия.ПервыйИгрок = Отправитель;
	НоваяСессия.СтатусИгры = Перечисления.СтатусыИгры.ОжиданиеВторогоИгрока;
	
	НовыйКод = НовыйКодДляСоединения();
	Пока СуществующиеКоды.Найти(НовыйКод) = 0 Цикл
		НовыйКод = НовыйКодДляСоединения();	
	КонецЦикла;
	НоваяСессия.КодДляСоединения = НовыйКод;

	НоваяСессия.Записать();	
	
	Шабл = "Сейчас вы в режиме ожидания соперника. Код для подключения соперника: %1";
	ТекстСообщения = СтрШаблон(Шабл, НовыйКод);
	ДополнительныеСвойства = ИнтеграцияТелеграм.ДополнительныеСвойстваИсходящегоСообщения();
	ДополнительныеСвойства.Клавиатура = КлавиатураСписокДействийВРежимеОжидания();
	ИнтеграцияТелеграм.ОтправитьСообщение(Отправитель, ТекстСообщения, ДополнительныеСвойства);
	
КонецПроцедуры

Функция НовыйКодДляСоединения()
	
	Генератор = Новый ГенераторСлучайныхЧисел();
	НовыйКодДляСоединения = Строка(Генератор.СлучайноеЧисло(1, 999999));
	
	Пока СтрДлина(НовыйКодДляСоединения) < 7 Цикл
		НовыйКодДляСоединения = "0" + НовыйКодДляСоединения;
	КонецЦикла;
	
	Возврат НовыйКодДляСоединения;
	
КонецФункции

Процедура СбросСессии(Отправитель)

	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ЧатИграСессии.Ссылка
		|ИЗ
		|	Справочник.ЧатИграСессии КАК ЧатИграСессии
		|ГДЕ
		|	ЧатИграСессии.ПервыйИгрок = &ТекИгрок";
	Запрос.УстановитьПараметр("ТекИгрок", Отправитель);
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		Объект = ВыборкаДетальныеЗаписи.Ссылка.ПолучитьОбъект();
		Объект.Удалить();	
	КонецЕсли;;

	ТекстСообщения = "Вы вышли из режима ожидания соперника. Выберите желаемое действие с клавиатуры";
	ДополнительныеСвойства = ИнтеграцияТелеграм.ДополнительныеСвойстваИсходящегоСообщения();
	ДополнительныеСвойства.Клавиатура = КлавиатураСписокДействийВЛобби();
	ИнтеграцияТелеграм.ОтправитьСообщение(Отправитель, ТекстСообщения, ДополнительныеСвойства);
	
КонецПроцедуры

Процедура НачатьСоединение(Отправитель)
	НоваяСессия = Справочники.ЧатИграПодключениеИгрока.СоздатьЭлемент();
	НоваяСессия.Игрок = Отправитель;
	НоваяСессия.Записать();
	
	СтрокиКлавиатуры = Новый Массив;
	
	СтрокаКлавиатуры = Новый Массив;
	Кнопка = Новый Структура;
	Кнопка.Вставить("text", "Вернуться назад");
	СтрокаКлавиатуры.Добавить(Кнопка);
	СтрокиКлавиатуры.Добавить(СтрокаКлавиатуры);
		
	Клавиатура = Новый Структура;
	Клавиатура.Вставить("keyboard", СтрокиКлавиатуры);
	Клавиатура.Вставить("resize_keyboard", Истина);	
	
	ДополнительныеСвойства = ИнтеграцияТелеграм.ДополнительныеСвойстваИсходящегоСообщения();
	ДополнительныеСвойства.Клавиатура = Клавиатура;
	ИнтеграцияТелеграм.ОтправитьСообщение(Отправитель, "Введите код подключения к игре", ДополнительныеСвойства);		
	
КонецПроцедуры

Процедура ПроверкаКодаПодключения(Отправитель, ВведеныйКод)
	
	ПроверочныйКод = Сред(ВведеныйКод, 1, 3) + " " + Сред(ВведеныйКод, 4);
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ЧатИграСессии.Ссылка КАК СсылкаСессии,
		|	ЧатИграСессии.ВторойИгрок КАК ВторойИгрок
		|ИЗ
		|	Справочник.ЧатИграСессии КАК ЧатИграСессии
		|ГДЕ
		|	ЧатИграСессии.КодДляСоединения = &КодДляСоединения
		|	ИЛИ ЧатИграСессии.КодДляСоединения = &КодДляСоединенияБезПробела";
	Запрос.УстановитьПараметр("КодДляСоединения", ПроверочныйКод);
	Запрос.УстановитьПараметр("КодДляСоединенияБезПробела", ВведеныйКод);
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
		
	Если Выборка.Следующий() Тогда
		Если Выборка.ВторойИгрок = 0 Тогда	
			ТекИгра = Выборка.СсылкаСессии.ПолучитьОбъект();
			Если ТекИгра.ПервыйИгрок = Отправитель Тогда
				ИнтеграцияТелеграм.ОтправитьСообщение(Отправитель, "Вы не можете подключиться к своей игре");
			Иначе
				СоединениеИгроков(Отправитель, ТекИгра);	 									
			КонецЕсли;
		Иначе
			ИнтеграцияТелеграм.ОтправитьСообщение(Отправитель, "Найденная игра уже запущена! Введите код другой игры");	
		КонецЕсли;
	Иначе
		ИнтеграцияТелеграм.ОтправитьСообщение(Отправитель, "Игра не найдена. Введите код ещё раз");
	КонецЕсли;
	
КонецПроцедуры

Процедура СоединениеИгроков(Отправитель, ТекИгра)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ЧатИграПодключениеИгрока.Ссылка
		|ИЗ
		|	Справочник.ЧатИграПодключениеИгрока КАК ЧатИграПодключениеИгрока
		|ГДЕ
		|	ЧатИграПодключениеИгрока.Игрок = &Игрок";	
	Запрос.УстановитьПараметр("Игрок", Отправитель);
	РезультатЗапроса = Запрос.Выполнить();
		
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		Объект = ВыборкаДетальныеЗаписи.Ссылка.ПолучитьОбъект();
		Объект.Удалить();
	КонецЕсли;	
				
	ТекИгра.ВторойИгрок = Отправитель;
	ТекИгра.СтатусИгры = Перечисления.СтатусыИгры.ОжиданиеГотовности;
				
	ГенераторЧисла = Новый ГенераторСлучайныхЧисел();
	ТекИгра.ПервыйХодПервыйИгрок = ГенераторЧисла.СлучайноеЧисло(0, 1);
		
	ТекИгра.Записать();
				
	ДополнительныеСвойства = ИнтеграцияТелеграм.ДополнительныеСвойстваИсходящегоСообщения();
	ДополнительныеСвойства.Клавиатура = КлавиатураСписокДействийВКонцеИгры("ОжиданиеГотовностиПерезапускуИгры");
	ИнтеграцияТелеграм.ОтправитьСообщение(ТекИгра.ПервыйИгрок,
				 						"Соперник подключен! Теперь вы можете играть!",
				  						ДополнительныеСвойства);
	ИнтеграцияТелеграм.ОтправитьСообщение(Отправитель,
				 						"Игра найдена. Теперь вы можете играть!",
				  						ДополнительныеСвойства);
	
	СтрокиКлавиатуры = Новый Массив;
	СтрокаКлавиатуры = Новый Массив;
	
	Кнопка = Новый Структура;
	Кнопка.Вставить("text", "Приготовиться");
	Кнопка.Вставить("callback_data", "Готовность");
	СтрокаКлавиатуры.Добавить(Кнопка);
	СтрокиКлавиатуры.Добавить(СтрокаКлавиатуры);
	
	Клавиатура = Новый Структура;
	Клавиатура.Вставить("inline_keyboard", СтрокиКлавиатуры);
	
	ДополнительныеСвойстваГот = ИнтеграцияТелеграм.ДополнительныеСвойстваИсходящегоСообщения();	
	ДополнительныеСвойстваГот.Клавиатура = Клавиатура;
	
	ИнтеграцияТелеграм.ОтправитьСообщение(ТекИгра.ПервыйИгрок, "Нажмите 'Приготовиться', чтобы начать игру",
														 ДополнительныеСвойстваГот);
	ИнтеграцияТелеграм.ОтправитьСообщение(Отправитель, "Нажмите 'Приготовиться', чтобы начать игру",
														 ДополнительныеСвойстваГот);	
КонецПроцедуры

Процедура СбросПодключения(Отправитель)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ЧатИграПодключениеИгрока.Ссылка
		|ИЗ
		|	Справочник.ЧатИграПодключениеИгрока КАК ЧатИграПодключениеИгрока
		|ГДЕ
		|	ЧатИграПодключениеИгрока.Игрок = &Игрок";	
	Запрос.УстановитьПараметр("Игрок", Отправитель);
	РезультатЗапроса = Запрос.Выполнить();

	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		Объект = ВыборкаДетальныеЗаписи.Ссылка.ПолучитьОбъект();
		Объект.Удалить();
	КонецЕсли;
	
	ДополнительныеСвойства = ИнтеграцияТелеграм.ДополнительныеСвойстваИсходящегоСообщения();
	ДополнительныеСвойства.Клавиатура = КлавиатураСписокДействийВЛобби();
	ИнтеграцияТелеграм.ОтправитьСообщение(Отправитель, "Выберите действие с помощью клавиатуры", ДополнительныеСвойства);	

КонецПроцедуры

Процедура ОбработатьНажатияКлеткиКнопки(НажатиеКнопки)
	Если Не НажатиеКнопки.Свойство("from") Тогда
		Возврат;
	КонецЕсли;
	
	Если Не НажатиеКнопки.Свойство("data") Тогда
		Возврат;
	КонецЕсли;
	
	Получатель = НажатиеКнопки.from.id;
	ДопИнфо = НажатиеКнопки.data;
	ИдентификаторСообщения = НажатиеКнопки.message.message_id;
	
	СостояниеИгрока = ПроверкаСостоянияИгрока(Получатель);
	Если СостояниеИгрока = "Игра" 
			ИЛИ (СостояниеИгрока = "ОжиданиеГотовности" и ДопИнфо = "Готовность") 
				ИЛИ (СостояниеИгрока = "ОжиданиеГотовностиПерезапускуИгры" и ДопИнфо = "ГотовностьКПерезапуску") Тогда
		Запрос = Новый Запрос;
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ЧатИграСессии.Ссылка КАК СсылкаНаИгру
			|ИЗ
			|	Справочник.ЧатИграСессии КАК ЧатИграСессии
			|ГДЕ
			|	ЧатИграСессии.ПервыйИгрок = &Игрок
			|	ИЛИ ЧатИграСессии.ВторойИгрок = &Игрок";
		Запрос.УстановитьПараметр("Игрок", Получатель);
		РезультатЗапроса = Запрос.Выполнить();
		
		Выборка = РезультатЗапроса.Выбрать();
		Если Выборка.Следующий() Тогда 		
			ОбъектИгра = Выборка.СсылкаНаИгру.ПолучитьОбъект();
			Если не (ОбъектИгра.ГотовностьИгрока1 и ОбъектИгра.ГотовностьИгрока2) Тогда
				Если ОбъектИгра.ПервыйИгрок = Получатель Тогда
					ОбъектИгра.ДанныеСообщенияПервогоИгрока = Новый ХранилищеЗначения(ИдентификаторСообщения);
					ОбъектИгра.ГотовностьИгрока1 = Истина;
					ИнтеграцияТелеграм.РедактироватьСообщение(Получатель, ИдентификаторСообщения,
															 "Теперь вы готовы к игре, ожидайте готовность соперника!");
					ОбъектИгра.Записать();
					ПроверкаНачалаИгры(ОбъектИгра, Получатель);	
				Иначе
					ОбъектИгра.ДанныеСообщенияВторогоИгрока = Новый ХранилищеЗначения(ИдентификаторСообщения);
					ОбъектИгра.ГотовностьИгрока2 = Истина;
					ИнтеграцияТелеграм.РедактироватьСообщение(Получатель, ИдентификаторСообщения, 
															"Теперь вы готовы к игре, ожидайте готовность соперника!");	
					ОбъектИгра.Записать();
					ПроверкаНачалаИгры(ОбъектИгра, Получатель);
				КонецЕсли;
			Иначе
				ОбработкаХода(ОбъектИгра, ДопИнфо, Получатель);	
			КонецЕсли;	
			
		КонецЕсли;	
	КонецЕсли;
КонецПроцедуры

Процедура ПроверкаНачалаИгры(ОбъектИгра, Получатель)
	
	Если ОбъектИгра.ДанныеСообщенияПервогоИгрока.Получить()<>Неопределено 
			и ОбъектИгра.ДанныеСообщенияВторогоИгрока.Получить()<>Неопределено Тогда
		
		ОбъектИгра.СтатусИгры = Перечисления.СтатусыИгры.Игра;
		ОбъектИгра.Записать();
				
		ДополнительныеСвойства = ИнтеграцияТелеграм.ДополнительныеСвойстваИсходящегоСообщения();	
		ДополнительныеСвойства.Клавиатура = ВстроеннаяКлавиатураНачальнойСетки();
		
		ХодПервого = ОбъектИгра.ПервыйХодПервыйИгрок;
		
		Если ХодПервого Тогда	
			Текст1 = "Ваш ход! Нажмите на любую клетку в данном сообщении.";
			Текст2 = "Ход вашего соперника! Ожидайте.";
		Иначе
			Текст1 = "Ход вашего соперника! Ожидайте.";
			Текст2 = "Ваш ход! Нажмите на любую клетку в данном сообщении.";
		КонецЕсли;
			
		ИнтеграцияТелеграм.РедактироватьСообщение(ОбъектИгра.ПервыйИгрок, 
													ОбъектИгра.ДанныеСообщенияПервогоИгрока.Получить(),
													Текст1, ДополнительныеСвойства);
		ИнтеграцияТелеграм.РедактироватьСообщение(ОбъектИгра.ВторойИгрок, 
													ОбъектИгра.ДанныеСообщенияВторогоИгрока.Получить(),
													Текст2, ДополнительныеСвойства);
	КонецЕсли;
			
КонецПроцедуры

Функция ВстроеннаяКлавиатураНачальнойСетки()
	
	ИндексКнопки = 0;
	СтрокиКлавиатуры = Новый Массив;
	СтрокаКлавиатуры = Новый Массив;
	
	Пока СтрокиКлавиатуры.Количество() <> 4 Цикл
	
		ИндексКнопки = ИндексКнопки + 1;
		
		ДанныеКнопки = Строка(СтрокиКлавиатуры.Количество()) + Строка(ИндексКнопки);
		
		Кнопка = Новый Структура;
		Кнопка.Вставить("text", " ");
		Кнопка.Вставить("callback_data", ДанныеКнопки);
		
		СтрокаКлавиатуры.Добавить(Кнопка);
		
		Если ИндексКнопки >= 4 Тогда
			ИндексКнопки = 0;
			СтрокиКлавиатуры.Добавить(СтрокаКлавиатуры);
			СтрокаКлавиатуры = Новый Массив;
		КонецЕсли;
		
	КонецЦикла;	
	
	Клавиатура = Новый Структура;
	Клавиатура.Вставить("inline_keyboard", СтрокиКлавиатуры);
	
	Возврат Клавиатура;
	
КонецФункции

Процедура ОбработкаХода(ОбъектИгра, ДопИнфо, Получатель)
	
	КоличествоТекХодов = ОбъектИгра.Ходы.Количество();
	
	Табличка = Новый Структура;
	Для каждого ТекСтрока из ОбъектИгра.Ходы Цикл
		Табличка.Вставить("З" + ТекСтрока.Ход, 0);
	КонецЦикла;	
		
	Если Табличка.Свойство("З" + ДопИнфо) ТОгда
		Возврат;
	КонецЕсли;
			
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ЧатИграСессииХоды.Игрок как Игрок,
		|	ЧатИграСессииХоды.КрестикНолик как КрестикНолик
		|ИЗ
		|	Справочник.ЧатИграСессии.Ходы КАК ЧатИграСессииХоды
		|ГДЕ
		|	ЧатИграСессииХоды.Игрок = &Игрок1
		|	ИЛИ ЧатИграСессииХоды.Игрок = &Игрок2
		|
		|УПОРЯДОЧИТЬ ПО
		|	ЧатИграСессииХоды.НомерСтроки УБЫВ";
	Запрос.УстановитьПараметр("Игрок2", ОбъектИгра.ПервыйИгрок);
	Запрос.УстановитьПараметр("Игрок1", ОбъектИгра.ВторойИгрок);
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	Если Выборка.Следующий() Тогда
	КонецЕсли;

	Если (Выборка.Игрок <> Получатель и КоличествоТекХодов > 0)
		или (КоличествоТекХодов = 0 и ((ОбъектИгра.ПервыйИгрок = Получатель и ОбъектИгра.ПервыйХодПервыйИгрок)
							 или (ОбъектИгра.ВторойИгрок = Получатель и НЕ ОбъектИгра.ПервыйХодПервыйИгрок) )) Тогда
							 	
		ОбъектИгра.СчетчикХодов = ОбъектИгра.СчетчикХодов + 1;
		
		Если КоличествоТекХодов = 0 Тогда
			НовыйХод = ОбъектИгра.Ходы.Добавить();
			НовыйХод.Игрок = Получатель;
			НовыйХод.Ход = ДопИнфо;
			НовыйХод.КрестикНолик = "Х";
			ОбъектИгра.Записать();
		Иначе
			НовыйХод = ОбъектИгра.Ходы.Добавить();
			НовыйХод.Игрок = Получатель;
			НовыйХод.Ход = ДопИнфо;
			Если Выборка.КрестикНолик = "О" Тогда
				НовыйХод.КрестикНолик = "Х";
			Иначе
				НовыйХод.КрестикНолик = "О";
			КонецЕсли;
			ОбъектИгра.Записать();
		КонецЕсли;
		
		КоличествоТХодов = ОбъектИгра.Ходы.Количество();
		
		УдалитьНадо = Ложь;
		Для каждого Строчка Из ОбъектИгра.Ходы Цикл
			Если Строчка.НомерСтроки = 1 и КоличествоТХодов = 12 Тогда
				ПоследняяДоВыбыванияКлетка = Строчка.Ход;
	        	Прервать;
			ИначеЕсли Строчка.НомерСтроки = 1 и КоличествоТХодов = 13 Тогда
	        	УдалитьНадо = Истина;
			ИначеЕсли Строчка.НомерСтроки = 2 Тогда
				ПоследняяДоВыбыванияКлетка = Строчка.Ход;
	        	Прервать;
	        КонецЕсли;
		КонецЦикла; 
		
		Если УдалитьНадо Тогда
			ОбъектИгра.Ходы.Удалить(0);
			ОбъектИгра.Записать();
		КонецЕсли;
		
		ТаблХодов = Новый Структура;
		Для каждого ТекСтрока из ОбъектИгра.Ходы Цикл
			ТаблХодов.Вставить("З" + ТекСтрока.Ход, ТекСтрока.КрестикНолик);
		КонецЦикла;	
		
		ДополнительныеСвойства = ИнтеграцияТелеграм.ДополнительныеСвойстваИсходящегоСообщения();	
		ДополнительныеСвойства.Клавиатура = ОбновитьПолеКлавиатуры(ОбъектИгра, ТаблХодов, ПоследняяДоВыбыванияКлетка);
			
		Если ОбъектИгра.ПервыйИгрок = Получатель и ОбъектИгра.ПервыйХодПервыйИгрок Тогда	
			Текст1 = "Ход вашего соперника! Ожидайте." + " (Вы - Х)";
			Текст2 = "Ваш ход! Нажмите на любую клетку в данном сообщении." + " (Вы - О)";
		ИначеЕсли ОбъектИгра.ПервыйИгрок = Получатель и не ОбъектИгра.ПервыйХодПервыйИгрок Тогда
			Текст1 = "Ход вашего соперника! Ожидайте." + " (Вы - О)";
			Текст2 = "Ваш ход! Нажмите на любую клетку в данном сообщении." + " (Вы - Х)";
		ИначеЕсли ОбъектИгра.ВторойИгрок = Получатель и ОбъектИгра.ПервыйХодПервыйИгрок Тогда
			Текст1 = "Ваш ход! Нажмите на любую клетку в данном сообщении." + " (Вы - Х)";
			Текст2 = "Ход вашего соперника! Ожидайте." + " (Вы - О)";		
		Иначе	
			Текст1 = "Ваш ход! Нажмите на любую клетку в данном сообщении." + " (Вы - О)";
			Текст2 = "Ход вашего соперника! Ожидайте." + " (Вы - Х)";
		КонецЕсли;
		
		ДанныеСообщения1 = ОбъектИгра.ДанныеСообщенияПервогоИгрока.Получить();
		ДанныеСообщения2 = ОбъектИгра.ДанныеСообщенияВторогоИгрока.Получить();
													
		ПобедительЛи = ОбработкаТекПоложенияИгры(ТаблХодов);
		Если ПобедительЛи Тогда
			
			Запрос = Новый Запрос;
			Запрос.Текст =
				"ВЫБРАТЬ ПЕРВЫЕ 1
				|	ЧатИграСессииХоды.Игрок как Игрок,
				|	ЧатИграСессииХоды.КрестикНолик как КрестикНолик
				|ИЗ
				|	Справочник.ЧатИграСессии.Ходы КАК ЧатИграСессииХоды
				|ГДЕ
				|	ЧатИграСессииХоды.Игрок = &Игрок1
				|	ИЛИ ЧатИграСессииХоды.Игрок = &Игрок2
				|
				|УПОРЯДОЧИТЬ ПО
				|	ЧатИграСессииХоды.НомерСтроки УБЫВ";
			Запрос.УстановитьПараметр("Игрок2", ОбъектИгра.ПервыйИгрок);
			Запрос.УстановитьПараметр("Игрок1", ОбъектИгра.ВторойИгрок);
			РезультатЗапроса = Запрос.Выполнить();
			
			СчетХодов = ОбъектИгра.СчетчикХодов;
				
			ОбъектИгра.СтатусИгры = Перечисления.СтатусыИгры.КонецИгры;
			ОбъектИгра.ДанныеСообщенияПервогоИгрока = Новый ХранилищеЗначения(Неопределено);
			ОбъектИгра.ДанныеСообщенияВторогоИгрока = Новый ХранилищеЗначения(Неопределено);
			ОбъектИгра.Ходы.Очистить();
			ОбъектИгра.ГотовностьИгрока1 = Ложь;
			ОбъектИгра.ГотовностьИгрока2 = Ложь;
			ОбъектИгра.СчетчикХодов = 0;
			
			ГенераторЧисла = Новый ГенераторСлучайныхЧисел();
			ОбъектИгра.ПервыйХодПервыйИгрок = ГенераторЧисла.СлучайноеЧисло(0, 1);
			
			ШаблонВин = "<b>Поздравляю, ВЫ победили!</b>%1Итоговый счёт текущей сессии - %2:%3! %4Выберите действие с клавиатуры.";
			ШаблонЛуз = "<b>К сожалению, ВЫ проиграли.</b>%1Итоговый счёт текущей сессии - %2:%3! %4Выберите действие с клавиатуры.";
			
			
			
			Выборка = РезультатЗапроса.Выбрать();
			Если Выборка.Следующий() Тогда
				Если не Выборка.Игрок = ОбъектИгра.ПервыйИгрок Тогда
					ОбъектИгра.КолвоПобедВторого = ОбъектИгра.КолвоПобедВторого + 1;
					Текст1 = СтрШаблон(ШаблонЛуз, Символы.ПС, ОбъектИгра.КолвоПобедПервого,
																 ОбъектИгра.КолвоПобедВторого, Символы.ПС);
					Текст2 = СтрШаблон(ШаблонВин, Символы.ПС, ОбъектИгра.КолвоПобедВторого,
																 ОбъектИгра.КолвоПобедПервого, Символы.ПС);
					
					Пользователь1 = Справочники.УчетныеЗаписиТелеграм.НайтиПоКоду(ОбъектИгра.ПервыйИгрок).Ссылка.ПолучитьОбъект();
					Пользователь1.ЧислоПоражений = Пользователь1.ЧислоПоражений + 1;
					Пользователь1.Записать();
					Пользователь2 = Справочники.УчетныеЗаписиТелеграм.НайтиПоКоду(ОбъектИгра.ВторойИгрок).Ссылка.ПолучитьОбъект();
					Пользователь2.ЧислоПобед = Пользователь2.ЧислоПобед + 1;
					Пользователь2.Записать();	
				Иначе
					ОбъектИгра.КолвоПобедПервого = ОбъектИгра.КолвоПобедПервого + 1;
					Текст1 = СтрШаблон(ШаблонВин, Символы.ПС, ОбъектИгра.КолвоПобедПервого,
																 ОбъектИгра.КолвоПобедВторого, Символы.ПС);
					Текст2 = СтрШаблон(ШаблонЛуз, Символы.ПС, ОбъектИгра.КолвоПобедВторого,
																 ОбъектИгра.КолвоПобедПервого, Символы.ПС);
																 
					Пользователь1 = Справочники.УчетныеЗаписиТелеграм.НайтиПоКоду(ОбъектИгра.ПервыйИгрок).Ссылка.ПолучитьОбъект();
					Пользователь1.ЧислоПобед = Пользователь1.ЧислоПобед + 1;
					Пользователь1.Записать();
					Пользователь2 = Справочники.УчетныеЗаписиТелеграм.НайтиПоКоду(ОбъектИгра.ВторойИгрок).Ссылка.ПолучитьОбъект();
					Пользователь2.ЧислоПоражений = Пользователь2.ЧислоПоражений + 1;
					Пользователь2.Записать();											 
				КонецЕсли;
			КонецЕсли;
			
			ОбъектИгра.Записать();
			
			
				
			ДополнительныеСвойстваГот = ИнтеграцияТелеграм.ДополнительныеСвойстваИсходящегоСообщения();	
			ДополнительныеСвойстваГот.Клавиатура = КлавиатураСписокДействийВКонцеИгры("КонецИгры");
			
			ИнтеграцияТелеграм.ОтправитьСообщение(ОбъектИгра.ПервыйИгрок, Текст1, ДополнительныеСвойстваГот);
			ИнтеграцияТелеграм.ОтправитьСообщение(ОбъектИгра.ВторойИгрок, Текст2, ДополнительныеСвойстваГот);
			
			Шаблон = "<b>Эта игра закончилась!</b> Количество ходов: %1.";
			ИнтеграцияТелеграм.РедактироватьСообщение(ОбъектИгра.ПервыйИгрок, 
													ДанныеСообщения1,
													СтрШаблон(Шаблон, СчетХодов), ДополнительныеСвойства);
			ИнтеграцияТелеграм.РедактироватьСообщение(ОбъектИгра.ВторойИгрок, 
													ДанныеСообщения2,
													СтрШаблон(Шаблон, СчетХодов), ДополнительныеСвойства);		
		Иначе
			ИнтеграцияТелеграм.РедактироватьСообщение(ОбъектИгра.ПервыйИгрок, 
													ДанныеСообщения1,
													Текст1, ДополнительныеСвойства);
			ИнтеграцияТелеграм.РедактироватьСообщение(ОбъектИгра.ВторойИгрок, 
													ДанныеСообщения2,
													Текст2, ДополнительныеСвойства);
		КонецЕсли;
			
		
	КонецЕсли;
	
КонецПроцедуры

Функция ОбновитьПолеКлавиатуры(ОбъектИгра, ТаблХодов, ПоследняяДоВыбыванияКлетка)
	
	ИндексКнопки = 0;
	СтрокиКлавиатуры = Новый Массив;
	СтрокаКлавиатуры = Новый Массив;
	
	КоличествоТекХодов = ОбъектИгра.Ходы.Количество();
	
	Если КоличествоТекХодов >= 12 Тогда
		
		Пока СтрокиКлавиатуры.Количество() <> 4 Цикл
	
			ИндексКнопки = ИндексКнопки + 1;
			
			Проверка = "З" + Строка(СтрокиКлавиатуры.Количество()) + Строка(ИндексКнопки);
			ДанныеКнопки = Строка(СтрокиКлавиатуры.Количество()) + Строка(ИндексКнопки);
			
			Если ТаблХодов.Свойство(Проверка) Тогда	
				Если ДанныеКнопки <> ПоследняяДоВыбыванияКлетка Тогда
					Кнопка = Новый Структура;
					Кнопка.Вставить("text", ТаблХодов[Проверка]);
					Кнопка.Вставить("callback_data", ДанныеКнопки);
				Иначе
					//Шаблон = "<b>%1</b>";  //СтрШаблон(Шаблон, ТаблХодов[Проверка]) Кнопка.Вставить("text", "✖");
					Кнопка = Новый Структура;
					
					Если ТаблХодов[Проверка] = "Х" Тогда
						Кнопка.Вставить("text", "❌");
					Иначе
						Кнопка.Вставить("text", "⭕");
					КонецЕсли;
					
					Кнопка.Вставить("callback_data", ДанныеКнопки);
				КонецЕсли;
			Иначе
				Кнопка = Новый Структура;
				Кнопка.Вставить("text", " ");
				Кнопка.Вставить("callback_data", ДанныеКнопки);
			КонецЕсли;	
			СтрокаКлавиатуры.Добавить(Кнопка);
			
			Если ИндексКнопки >= 4 Тогда
				ИндексКнопки = 0;
				СтрокиКлавиатуры.Добавить(СтрокаКлавиатуры);
				СтрокаКлавиатуры = Новый Массив;
			КонецЕсли;
		КонецЦикла;
		
	Иначе
		Пока СтрокиКлавиатуры.Количество() <> 4 Цикл
	
			ИндексКнопки = ИндексКнопки + 1;
			
			Проверка = "З" + Строка(СтрокиКлавиатуры.Количество()) + Строка(ИндексКнопки);
			ДанныеКнопки = Строка(СтрокиКлавиатуры.Количество()) + Строка(ИндексКнопки);
			
			Если ТаблХодов.Свойство(Проверка) Тогда	
				Кнопка = Новый Структура;
				Кнопка.Вставить("text", ТаблХодов[Проверка]);
				Кнопка.Вставить("callback_data", ДанныеКнопки);
			Иначе
				Кнопка = Новый Структура;
				Кнопка.Вставить("text", " ");
				Кнопка.Вставить("callback_data", ДанныеКнопки);
			КонецЕсли;	
			СтрокаКлавиатуры.Добавить(Кнопка);
			
			Если ИндексКнопки >= 4 Тогда
				ИндексКнопки = 0;
				СтрокиКлавиатуры.Добавить(СтрокаКлавиатуры);
				СтрокаКлавиатуры = Новый Массив;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Клавиатура = Новый Структура;
	Клавиатура.Вставить("inline_keyboard", СтрокиКлавиатуры);
	
	Возврат Клавиатура;
	
КонецФункции

Функция ОбработкаТекПоложенияИгры(ТаблХодов)
	
	ВсеВыигрПоз = Новый Массив; 
	
	ВыигрПоз = Новый Массив;
	ВыигрПоз.Добавить("З01");
	ВыигрПоз.Добавить("З02");
	ВыигрПоз.Добавить("З03");
	ВыигрПоз.Добавить("З04");
	ВсеВыигрПоз.Добавить(ВыигрПоз);
	
	ВыигрПоз = Новый Массив;
	ВыигрПоз.Добавить("З11");
	ВыигрПоз.Добавить("З12");
	ВыигрПоз.Добавить("З13");
	ВыигрПоз.Добавить("З14");
	ВсеВыигрПоз.Добавить(ВыигрПоз);
	
	ВыигрПоз = Новый Массив;
	ВыигрПоз.Добавить("З21");
	ВыигрПоз.Добавить("З22");
	ВыигрПоз.Добавить("З23");
	ВыигрПоз.Добавить("З24");
	ВсеВыигрПоз.Добавить(ВыигрПоз);
	
	ВыигрПоз = Новый Массив;
	ВыигрПоз.Добавить("З31");
	ВыигрПоз.Добавить("З32"); 
	ВыигрПоз.Добавить("З33");
	ВыигрПоз.Добавить("З34");
	ВсеВыигрПоз.Добавить(ВыигрПоз);
	
	ВыигрПоз = Новый Массив;
	ВыигрПоз.Добавить("З01");
	ВыигрПоз.Добавить("З11");
	ВыигрПоз.Добавить("З21");
	ВыигрПоз.Добавить("З31");
	ВсеВыигрПоз.Добавить(ВыигрПоз);
	
	ВыигрПоз = Новый Массив;
	ВыигрПоз.Добавить("З02");
	ВыигрПоз.Добавить("З12");
	ВыигрПоз.Добавить("З22");
	ВыигрПоз.Добавить("З32");
	ВсеВыигрПоз.Добавить(ВыигрПоз);
	
	ВыигрПоз = Новый Массив;
	ВыигрПоз.Добавить("З03");
	ВыигрПоз.Добавить("З13");
	ВыигрПоз.Добавить("З23");
	ВыигрПоз.Добавить("З33");
	ВсеВыигрПоз.Добавить(ВыигрПоз);
	
	ВыигрПоз = Новый Массив;
	ВыигрПоз.Добавить("З04");
	ВыигрПоз.Добавить("З14");
	ВыигрПоз.Добавить("З24");
	ВыигрПоз.Добавить("З34");
	ВсеВыигрПоз.Добавить(ВыигрПоз);
	
	ВыигрПоз = Новый Массив;
	ВыигрПоз.Добавить("З01");
	ВыигрПоз.Добавить("З12");
	ВыигрПоз.Добавить("З23");
	ВыигрПоз.Добавить("З34");
	ВсеВыигрПоз.Добавить(ВыигрПоз);
	
	ВыигрПоз = Новый Массив;
	ВыигрПоз.Добавить("З04");
	ВыигрПоз.Добавить("З13");
	ВыигрПоз.Добавить("З22");
	ВыигрПоз.Добавить("З31");
	ВсеВыигрПоз.Добавить(ВыигрПоз);
																			
	Для каждого ЗначениеВ из ВсеВыигрПоз Цикл
		Если ТаблХодов.Свойство(ЗначениеВ[0]) и ТаблХодов.Свойство(ЗначениеВ[1])
			и ТаблХодов.Свойство(ЗначениеВ[2]) и ТаблХодов.Свойство(ЗначениеВ[3]) Тогда
				Если ТаблХодов[ЗначениеВ[0]] = ТаблХодов[ЗначениеВ[1]]
						и ТаблХодов[ЗначениеВ[2]] = ТаблХодов[ЗначениеВ[3]]
							и ТаблХодов[ЗначениеВ[0]] = ТаблХодов[ЗначениеВ[2]] Тогда
					Возврат Истина;
				КонецЕсли;		
		КонецЕсли;
	КонецЦикла;	
	
	Возврат Ложь;
	
КонецФункции

Процедура СбросСессионнойИгры(Отправитель)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ЧатИграСессии.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ЧатИграСессии КАК ЧатИграСессии
		|ГДЕ
		|	ЧатИграСессии.ПервыйИгрок = &ТекИгрок
		|	ИЛИ ЧатИграСессии.ВторойИгрок = &ТекИгрок";
	Запрос.УстановитьПараметр("ТекИгрок", Отправитель);
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		Объект = ВыборкаДетальныеЗаписи.Ссылка.ПолучитьОбъект();
		
		ДополнительныеСвойства = ИнтеграцияТелеграм.ДополнительныеСвойстваИсходящегоСообщения();
		ДополнительныеСвойства.Клавиатура = КлавиатураСписокДействийВЛобби();
		
		ИнтеграцияТелеграм.ОтправитьСообщение(Отправитель, 
										"Вы вышли из игры. Выберите желаемое действие с клавиатуры",
										 ДополнительныеСвойства);
		Если Объект.ПервыйИгрок = Отправитель Тогда
			ИнтеграцияТелеграм.ОтправитьСообщение(Объект.ВторойИгрок,
										 "Ваш соперник вышел из игры. Выберите желаемое действие с клавиатуры",
										  ДополнительныеСвойства);
		Иначе
			ИнтеграцияТелеграм.ОтправитьСообщение(Объект.ПервыйИгрок,
										 "Ваш соперник вышел из игры. Выберите желаемое действие с клавиатуры",
										  ДополнительныеСвойства);
		КонецЕсли;
			
		Объект.Удалить();	
	КонецЕсли;;
	
КонецПроцедуры

Процедура НачатьИгруЗаново(Отправитель, СостояниеИгрока)
	
	Если СостояниеИгрока = "КонецИгры" Тогда
		Запрос = Новый Запрос;
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ЧатИграСессии.Ссылка как СсылкаСессии
			|ИЗ
			|	Справочник.ЧатИграСессии КАК ЧатИграСессии
			|ГДЕ
			|	ЧатИграСессии.ПервыйИгрок = &Игрок
			|	ИЛИ ЧатИграСессии.ВторойИгрок = &Игрок";
		Запрос.УстановитьПараметр("Игрок", Отправитель);
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		Если ВыборкаДетальныеЗаписи.Следующий() Тогда
			ТекИгра = ВыборкаДетальныеЗаписи.СсылкаСессии.ПолучитьОбъект();
		КонецЕсли;
	
		СтрокиКлавиатуры = Новый Массив;
		СтрокаКлавиатуры = Новый Массив;
		
		ТекИгра.СтатусИгры = Перечисления.СтатусыИгры.ОжиданиеГотовностиПерезапускуИгры;
		ТекИгра.Записать();
		
		Кнопка = Новый Структура;
		Кнопка.Вставить("text", "Приготовиться");
		Кнопка.Вставить("callback_data", "ГотовностьКПерезапуску");
		СтрокаКлавиатуры.Добавить(Кнопка);
		СтрокиКлавиатуры.Добавить(СтрокаКлавиатуры);
		
		Клавиатура = Новый Структура;
		Клавиатура.Вставить("inline_keyboard", СтрокиКлавиатуры);
		
		ДополнительныеСвойстваГот = ИнтеграцияТелеграм.ДополнительныеСвойстваИсходящегоСообщения();	
		ДополнительныеСвойстваГот.Клавиатура = Клавиатура;
		
		ИнтеграцияТелеграм.ОтправитьСообщение(ТекИгра.ПервыйИгрок, "Нажмите 'Приготовиться', чтобы начать новую игру",
																 ДополнительныеСвойстваГот);
		ИнтеграцияТелеграм.ОтправитьСообщение(ТекИгра.ВторойИгрок, "Нажмите 'Приготовиться', чтобы начать новую игру",
																 ДополнительныеСвойстваГот);
	Иначе
		ИнтеграцияТелеграм.ОтправитьСообщение(Отправитель, "Сейчас невозможно начать новую игру! Дождитесь конца игры!");
	КонецЕсли;
															 	
КонецПроцедуры

Процедура ПредупреждениеОСообщении(Отправитель) // наверное не надо

	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ЧатИграСессии.Ссылка как Ссылка
		|ИЗ
		|	Справочник.ЧатИграСессии КАК ЧатИграСессии
		|ГДЕ
		|	ЧатИграСессии.ПервыйИгрок = &Игрок
		|	ИЛИ ЧатИграСессии.ВторойИгрок = &Игрок";
	Запрос.УстановитьПараметр("Игрок", Отправитель);
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	Если Выборка.Следующий() Тогда
		ОбъектИгра = Выборка.Ссылка.ПолучитьОбъект(); 
		Если Отправитель = ОбъектИгра.ПервыйИгрок Тогда
			ИнтеграцияТелеграм.ОтправитьСообщение(ОбъектИгра.ПервыйИгрок,
				"Пожалуйтста, НЕ ПИШИТЕ в чате без необходимости, для игры используется только вышеуказанно поле");
		Иначе
			ИнтеграцияТелеграм.ОтправитьСообщение(ОбъектИгра.ВторойИгрок,
				"Пожалуйтста, НЕ ПИШИТЕ в чате без необходимости, для игры используется только вышеуказанно поле");
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

Процедура ПредложитьВыбратьДействия(Отправитель, Состояние)
	
	ТекстСообщения = "Действий не найдено!";
	ДополнительныеСвойства = ИнтеграцияТелеграм.ДополнительныеСвойстваИсходящегоСообщения();	
	Если Состояние = "ВнеИгры" Тогда
		ТекстСообщения = "Выберите действие с помощью клавиатуры";
		ДополнительныеСвойства.Клавиатура = КлавиатураСписокДействийВЛобби();
		ИнтеграцияТелеграм.ОтправитьСообщение(Отправитель, ТекстСообщения, ДополнительныеСвойства);
	ИначеЕсли Состояние = "ОжиданиеГотовностиПерезапускуИгры" Тогда
		
	ИначеЕсли Состояние = "КонецИгры" Тогда
		ТекстСообщения = "Выберите действие с помощью клавиатуры";
		ДополнительныеСвойства.Клавиатура = КлавиатураСписокДействийВКонцеИгры(Состояние);
		ИнтеграцияТелеграм.ОтправитьСообщение(Отправитель, ТекстСообщения, ДополнительныеСвойства);
	ИначеЕсли Состояние = "ОжиданиеИгры" Тогда
		Шабл = "Сейчас вы в режиме ожидания соперника. Код для подключения соперника: %1";
		ТекстСообщения = СтрШаблон(Шабл, ПолучитьКодПодключения(Отправитель));
		ДополнительныеСвойства.Клавиатура = КлавиатураСписокДействийВРежимеОжидания();
		ИнтеграцияТелеграм.ОтправитьСообщение(Отправитель, ТекстСообщения, ДополнительныеСвойства);
	КонецЕсли;
			
	
КонецПроцедуры

Функция КлавиатураСписокДействийВЛобби()
	
	СтрокиКлавиатуры = Новый Массив;

	СтрокаКлавиатуры = Новый Массив;
	Кнопка = Новый Структура;
	Кнопка.Вставить("text", "Мой профиль");
	СтрокаКлавиатуры.Добавить(Кнопка);
	СтрокиКлавиатуры.Добавить(СтрокаКлавиатуры);
	
	СтрокаКлавиатуры = Новый Массив;
	Кнопка = Новый Структура;
	Кнопка.Вставить("text", "Создать новую игру");
	СтрокаКлавиатуры.Добавить(Кнопка);
	СтрокиКлавиатуры.Добавить(СтрокаКлавиатуры);
		
	СтрокаКлавиатуры = Новый Массив;
	Кнопка = Новый Структура;
	Кнопка.Вставить("text", "Присоединиться к игре");
	СтрокаКлавиатуры.Добавить(Кнопка);
	СтрокиКлавиатуры.Добавить(СтрокаКлавиатуры);
		
	Клавиатура = Новый Структура;
	Клавиатура.Вставить("keyboard", СтрокиКлавиатуры);
	Клавиатура.Вставить("resize_keyboard", Истина);
	
	Возврат Клавиатура;	

КонецФункции

Функция КлавиатураСписокДействийВКонцеИгры(Состояние, ДопСтрокиВперед = Неопределено)
	
	СтрокиКлавиатуры = Новый Массив;
	
	Если Состояние <> "ОжиданиеГотовностиПерезапускуИгры" Тогда
		СтрокаКлавиатуры = Новый Массив;
		Кнопка = Новый Структура;
		Кнопка.Вставить("text", "Начать заново");
		СтрокаКлавиатуры.Добавить(Кнопка);
		СтрокиКлавиатуры.Добавить(СтрокаКлавиатуры);
	КонецЕсли;
		
	СтрокаКлавиатуры = Новый Массив;
	Кнопка = Новый Структура;
	Кнопка.Вставить("text", "Выйти из игры");
	СтрокаКлавиатуры.Добавить(Кнопка);
	СтрокиКлавиатуры.Добавить(СтрокаКлавиатуры);
		
	Клавиатура = Новый Структура;
	
	Если ДопСтрокиВперед <> Неопределено ТОгда
		Для каждого Строка Из ДопСтрокиВперед Цикл
			Клавиатура.Вставить(Строка.Ключ, Строка.Значение);		
		КонецЦикла;
	КонецЕсли;	
	Клавиатура.Вставить("keyboard", СтрокиКлавиатуры);
	Клавиатура.Вставить("resize_keyboard", Истина);
	
	Возврат Клавиатура;	

КонецФункции

Функция КлавиатураСписокДействийВРежимеОжидания()
	
	СтрокиКлавиатуры = Новый Массив;
	
	СтрокаКлавиатуры = Новый Массив;
	Кнопка = Новый Структура;
	Кнопка.Вставить("text", "Выйти из режима ожидания");
	СтрокаКлавиатуры.Добавить(Кнопка);
	СтрокиКлавиатуры.Добавить(СтрокаКлавиатуры);
		
	Клавиатура = Новый Структура;
	Клавиатура.Вставить("keyboard", СтрокиКлавиатуры);
	Клавиатура.Вставить("resize_keyboard", Истина);	
	
	Возврат Клавиатура;	

КонецФункции

Функция ПолучитьКодПодключения(Отправитель)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ЧатИграСессии.КодДляСоединения
		|ИЗ
		|	Справочник.ЧатИграСессии КАК ЧатИграСессии
		|ГДЕ
		|	ЧатИграСессии.ПервыйИгрок = &ТекИгрок
		|	ИЛИ ЧатИграСессии.ВторойИгрок = &ТекИгрок";
	
	Запрос.УстановитьПараметр("ТекИгрок", Отправитель);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.КодДляСоединения;
	Иначе
		Возврат 0;		
	КонецЕсли;
	
КонецФункции
	
#КонецОбласти
